plugins {
    id 'java'
    id 'com.modrinth.minotaur' version '2.8.7'
}

group = 'com.wavedefense'
version = '2.0.0'

repositories {
    mavenCentral()
    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT'
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
}

tasks.withType(JavaCompile).configureEach {
    options.release = 21
    options.encoding = 'UTF-8'
}

jar {
    from("LICENSE") {
        rename { "${it}_wavedefense" }
    }
}

processResources {
    inputs.property "version", project.version
    filesMatching("plugin.yml") {
        expand "version": project.version
    }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN") ?: project.findProperty("modrinth_token") ?: ""
    projectId = "wave-defend"
    versionNumber = "${version}"
    versionName = "Wave Defense ${version} (Paper)"
    versionType = "release"
    uploadFile = jar
    gameVersions = ["1.21.4"]
    loaders = ["paper"]
    changelog = """
## v2.0.0 - Paper Plugin Conversion

### Changes
- Converted from Fabric mod to Paper plugin
- Added Player vs Player arena mode
- Added potions to Mace kit
- Removed Wave Defense mode (Arena + Survival only)
- All 8 kits preserved: Mace, Sword, Axe, Bow, Crystal, UHC, Shield, Potion
"""
}
